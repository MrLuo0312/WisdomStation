ext.runAsApp = true
apply from: 'https://raw.githubusercontent.com/luckybilly/CC/master/cc-settings.gradle'

android {
    compileSdkVersion rootProject.compileVersion

    defaultConfig {
        applicationId "com.winsion.dispatch"
        minSdkVersion rootProject.minVersion
        targetSdkVersion rootProject.targetVersion
        versionCode 1
        versionName "0.0.1"

        ndk {
            abiFilters 'armeabi-v7a'
        }
    }

    signingConfigs {
        release {
            keyAlias 'dispatchkey'
            keyPassword 'winsion123'
            storeFile file('C:/Users/10295/Desktop/dispatchkey.jks')
            storePassword 'winsion123'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "dispatch_${defaultConfig.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion rootProject.supportVersion
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':component_basic')
    implementation project(':component_user')
    implementation project(':component_task')
    implementation project(':component_remind')
    implementation project(':component_scene')
    implementation project(':component_aad')
    implementation project(':component_contact')
    implementation project(':component_scanner')
    // 内存监测
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
}
