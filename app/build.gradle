apply plugin: 'com.android.application'
apply plugin: 'io.objectbox'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.winsion.wisdomstation"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    signingConfigs {
        release {
            keyAlias 'wisdomstationkey'
            keyPassword 'winsion123'
            storeFile file('C:/Users/10295/Desktop/wisdomstationkey.jks')
            storePassword 'winsion123'
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    outputFileName = "智慧车站_${defaultConfig.versionName}_${releaseTime()}.apk"
                }
            }
        }
    }
}

static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '26.1.0'
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    // 网络
    implementation 'com.lzy.net:okgo:3.0.1'
    implementation 'com.lzy.net:okrx2:2.0.1'
    implementation 'com.lzy.net:okserver:2.0.1'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    // JSON
    implementation 'com.alibaba:fastjson:1.2.33'
    // 数据绑定
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    // MQ
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    // 图片加载
    implementation 'com.github.bumptech.glide:glide:4.4.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.4.0'
    // Adapter
    implementation 'com.zhy:base-adapter:3.0.3'
    implementation 'com.zhy:base-rvadapter:3.0.3'
    // GridLayout兼容库
    implementation 'com.android.support:gridlayout-v7:26.1.0'
    // 事件传递
    implementation 'org.greenrobot:eventbus:3.1.1'
    // 选择器
    implementation 'com.contrarywind:Android-PickerView:3.2.5'
}
